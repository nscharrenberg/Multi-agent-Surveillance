plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id 'org.beryx.jlink' version '2.24.1'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

group 'com.nscharrenberg.um'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.nscharrenberg.um.multiagentsurveillance'
    mainClass = 'com.nscharrenberg.um.multiagentsurveillance.Application'
}

sonarqube {
    properties {
        property "sonar.projectKey", "nscharrenberg_Multi-agent-Surveillance"
        property "sonar.organization", "nscharrenberg"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}